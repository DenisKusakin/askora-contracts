#include "constants.fc";
#include "op-codes.fc";
#include "imports/stdlib.fc";

const int min_account_balance = 5 * TON_STEP;

(cell, cell, cell, slice, slice) load_data() impure {
    slice ds = get_data().begin_parse();
    (cell account_code, cell question_code, cell question_ref_code) = (ds~load_ref(), ds~load_ref(), ds~load_ref());
    if (ds.slice_bits() > 0) {
        return (account_code, question_code, question_ref_code, ds~load_msg_addr(), ds~load_msg_addr());
    } else {
        return (account_code, question_code, question_ref_code, null(), null());
    }
}

() store_data(cell account_code, cell question_code, cell question_ref_code, slice owner, slice sponsor_address) impure {
    set_data(
        begin_cell()
            .store_ref(account_code)
            .store_ref(question_code)
            .store_ref(question_ref_code)
            .store_slice(owner)
            .store_slice(sponsor_address)
            .end_cell()
    );
}

cell calculate_account_state_init(slice owner_addr, cell account_code, cell question_code, cell question_ref_code) impure {
    cell data = begin_cell()
        .store_slice(owner_addr)
        .store_slice(my_address())
        .store_ref(question_code)
        .store_ref(question_ref_code)
        .end_cell();

    return begin_cell()
        .store_uint(0, 2)
        .store_dict(account_code)
        .store_dict(data)
        .store_uint(0, 1)
        .end_cell();
}

cell calculate_question_state_init(slice parent_addr, int id, cell question_code) {
    cell data = begin_cell()
        .store_slice(parent_addr)
        .store_uint(id, 32)
        .end_cell();

    return begin_cell()
        .store_uint(0, 2)
        .store_dict(question_code)
        .store_dict(data)
        .store_uint(0, 1)
        .end_cell();
}

slice calculate_adddress_by_stateinit(cell state_init) {
    return begin_cell().store_uint(4, 3)
        .store_int(0, 8)
        .store_uint(cell_hash(state_init), 256)
        .end_cell()
        .begin_parse();
}

() deploy_account(slice owner, cell account_code, cell question_code, cell question_ref_code, int min_price, cell description, int is_sponsored, int amount, int mode) impure {
    cell state_init = calculate_account_state_init(owner, account_code, question_code, question_ref_code);
    slice account_address = calculate_adddress_by_stateinit(state_init);

    var msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(account_address)
        .store_coins(amount)
        .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
        .store_ref(state_init)
        .store_ref(begin_cell()
            .store_uint(op::deploy_account, 32)
            .store_coins(min_price)
            .store_ref(description)
            .store_int(is_sponsored, 32)
            .end_cell()
        );

    send_raw_message(msg.end_cell(), mode);
}

slice get_account_addr(slice owner) method_id {
    slice ds = get_data().begin_parse();
    var (account_code, question_code, question_ref_code) = (ds~load_ref(), ds~load_ref(), ds~load_ref());
    cell state_init = calculate_account_state_init(owner, account_code, question_code, question_ref_code);
    slice account_address = calculate_adddress_by_stateinit(state_init);

    return account_address;
}

cell get_account_code() method_id {
    slice ds = get_data().begin_parse();
    var (_, account_code) = (ds~load_msg_addr(), ds~load_ref());

    return account_code;
}

() send_coins(slice addr, int amount, int op) impure {
    var msg = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(addr)
        .store_coins(amount)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op, 32);

    send_raw_message(msg.end_cell(), 0);
}

() send_update_price(slice account_addr, int new_price) impure {
    var msg = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(account_addr)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::update_price, 32)
        .store_coins(new_price)
        .store_int(true, 32)
        .end_cell();

    send_raw_message(msg, 64);
}

() send_update_description(slice account_addr, cell new_description) impure {
    var msg = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(account_addr)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::update_description, 32)
        .store_ref(new_description)
        .store_int(true, 32)
        .end_cell();

    send_raw_message(msg, 64);
}

() send_reply(slice question_addr, cell reply_content) impure {
    var msg = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(question_addr)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::reply, 32)
        .store_ref(reply_content)
        .store_int(true, 32)
        .end_cell();

    send_raw_message(msg, 64);
}

() send_reject(slice question_addr) impure {
    var msg = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(question_addr)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::reject, 32)
        .store_int(true, 32)
        .end_cell();

    send_raw_message(msg, 64);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    ;; if (in_msg_body.slice_empty?()) {
    ;;     return ();
    ;; }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) {
        return ();
    }
    slice sender_addr = cs~load_msg_addr();

    cs~load_msg_addr(); ;; skip dst
    cs~load_coins(); ;; skip value
    cs~skip_bits(1); ;; skip extracurrency collection
    cs~load_coins(); ;; skip ihr_fee

    int op = in_msg_body~load_uint(32);
    (cell account_code, cell question_code, cell question_ref_code, slice owner, slice sponsor_address) = load_data();
    if (op == op::deploy_root & null?(owner)) {
        slice sponsor_address_from_msg = in_msg_body~load_msg_addr();
        store_data(account_code, question_code, question_ref_code, sender_addr, sponsor_address_from_msg);
        return ();
    }
    if (op == op::create_account) {
        int min_price = in_msg_body~load_coins();
        cell description = in_msg_body~load_ref();
        deploy_account(sender_addr, account_code, question_code, question_ref_code, min_price, description, false, 0, 64);
        return ();
    }
    if (op == op::change_sponsor) {
        throw_unless(403, equal_slices(owner, sender_addr));
        slice new_sponsor = in_msg_body~load_msg_addr();
        store_data(account_code, question_code, question_ref_code, sender_addr, new_sponsor);
        return ();
    }
    ;; Sponsored transaction could be sent only from a trusted wallet
    ;; such transactions supposed to be sent from the backend on behalf of user,
    ;; eligibility of this action should be checked on the backend using tonproof
    if (op == op::create_account_sponsored) {
        throw_unless(403, equal_slices(sender_addr, sponsor_address));
        slice account_owner = in_msg_body~load_msg_addr();
        int min_price = in_msg_body~load_coins();
        cell description = in_msg_body~load_ref();
        deploy_account(account_owner, account_code, question_code, question_ref_code, min_price, description, true, 0, 64);
        return ();
    }
    if (op == op::update_price_sponsored) {
        throw_unless(403, equal_slices(sender_addr, sponsor_address));
        slice account_owner = in_msg_body~load_msg_addr();
        int min_price = in_msg_body~load_coins();

        var account_state_init = calculate_account_state_init(account_owner, account_code, question_code, question_ref_code);
        var account_addr = calculate_adddress_by_stateinit(account_state_init);
        send_update_price(account_addr, min_price);
        return ();
    }
    if (op == op::update_description_sponsored) {
        throw_unless(403, equal_slices(sender_addr, sponsor_address));
        slice account_owner = in_msg_body~load_msg_addr();
        cell new_description = in_msg_body~load_ref();

        var account_state_init = calculate_account_state_init(account_owner, account_code, question_code, question_ref_code);
        var account_addr = calculate_adddress_by_stateinit(account_state_init);

        send_update_description(account_addr, new_description);
        return ();
    }
    if (op == op::reply_sponsored) {
        throw_unless(403, equal_slices(sender_addr, sponsor_address));
        slice account_owner = in_msg_body~load_msg_addr();
        int q_id = in_msg_body~load_int(32);
        cell reply_content = in_msg_body~load_ref();

        var account_state_init = calculate_account_state_init(account_owner, account_code, question_code, question_ref_code);
        var account_addr = calculate_adddress_by_stateinit(account_state_init);

        var question_state_init = calculate_question_state_init(account_addr, q_id, question_code);
        var question_addr = calculate_adddress_by_stateinit(question_state_init);

        send_reply(question_addr, reply_content);

        return ();
    }
    if (op == op::reject_sponsored) {
        throw_unless(403, equal_slices(sender_addr, sponsor_address));
        slice account_owner = in_msg_body~load_msg_addr();
        int q_id = in_msg_body~load_int(32);

        var account_state_init = calculate_account_state_init(account_owner, account_code, question_code, question_ref_code);
        var account_addr = calculate_adddress_by_stateinit(account_state_init);

        var question_state_init = calculate_question_state_init(account_addr, q_id, question_code);
        var question_addr = calculate_adddress_by_stateinit(question_state_init);

        send_reject(question_addr);

        return ();
    }
    if (op == op::service_fee) {
        return ();
    }
    if (op == op::excess_return) {
        return ();
    }
    if (op == op::withdraw_safe) {
        throw_unless(403, equal_slices(sender_addr, owner));
        int to_withdraw = my_balance - min_account_balance;
        send_coins(owner, to_withdraw, op::paynquiry_withdrawal);
        return ();
    }
    ;;The only reason to send and handle these operations - to have all the notifications in one contract
    ;;so using TON Api we could easily subscribe to transactions
    ;;There is no need to check whether message came from the real account, it will be done on the backend
    if (op == op::question_created_notification) {
        return ();
    }
    if (op == op::question_replied_notification) {
        return ();
    }
    if (op == op::question_rejected_notification) {
        return ();
    }

    throw(400);
}
